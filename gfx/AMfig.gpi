

set terminal postscript eps enhanced size 7,5.24 defaultplex \
   leveldefault mono dashed \
   dashlength 1.0 linewidth 2.0 butt noclip \
   palfuncparam 2000,0.003 \
   "Helvetica" 18 
#color solid 
set output "../VowelProcessingChapter/gfx/AMfig.eps"


#set term pngcairo size 700,524 enhanced font 'Verdana,10'
#set output 'AN_fc2000fm100.png'

set multiplot layout 2,2
unset ytics
set xtics out nomirror
set ylabel "Amplitude" font "Helvetica,16" offset -4,0
set xlabel "Time (ms)" font "Helvetica,16"
set label 1 "{/Helvetica-Bold=28 A}" at screen 0.0, screen 0.98
set samples 10000
plot [60:100] (1+sin(x*2*pi*0.1))*sin(x*2*pi*2) w l lc 'black', (1+sin(x*2*pi*0.1)) w l lw 2 lc 'black' ,-(1+sin(x*2*pi*0.1)) w l lw 2 lc 'black'



unset samples
set xlabel "Frequency (kHz)" font "Helvetica,16"
set ylabel "Power" font "Helvetica,16" offset -1,0
set yrange [0:1]
unset ytics
set xrange [0:2.5]
set label 1 "{/Helvetica-Bold=28 B}" at screen 0.5, screen 0.98

$spectrum << EOD
1.9 0.5
2 1
2.1  0.5
EOD
plot $spectrum  w impulses lc 'black', '' w p lc 'black'


set xlabel "Time (ms)" font "Helvetica,16"
set ylabel "Spike Count" font "Helvetica,16" offset 0,0
set ytics nomirror
set yrange [*:*]
set xrange [40:150]
set style fill solid
set label 1 "{/Helvetica-Bold=28 C}" at screen 0.0, screen 0.5

plot [60:100] '../VowelProcessingChapter/gfx/ANfc2000fm100/an.HSR.hist' u ($0/10):1 w boxes lc 'black'

#, \
#     [60:100] '../VowelProcessingChapter/gfx/ANfc2000fm100/an.HSR.33.hist' u ($0/10):1 w boxes lc 'yellow'

set style line 11 lc rgb '#808080' lt 1

set arrow 1 from 0.1,300 to  0.1,50  filled ls 11 # size screen 0.025,15,60
set arrow 2 from 0.2,50 to 0.2,5  filled ls 11 # size screen 0.025,15,60
set arrow 3 from 0.3,20 to  0.3,2  filled ls 11 # size screen 0.025,15,60
#set arrow 4 from 2,2 to 2,0.2  filled ls 11 # size screen 0.025,15,60


set ylabel "Power" font "Helvetica,16" offset -1,0
set xlabel "Frequency (kHz)" font "Helvetica,16"
unset ytics 
unset style fill
set logscale y 10
set key
set label 1 "{/Helvetica-Bold=28 D}" at screen 0.5, screen 0.5

 plot [*:2.5] '../VowelProcessingChapter/gfx/ANfc2000fm100/an.HSR.pow' u ($0/50):1 t 'All HSR units' w l lc 'black', \
     [*:2.5] '../VowelProcessingChapter/gfx/ANfc2000fm100/an.HSR.33.pow' u ($0/50):1 t 'HSR unit 33 (CF 1979 Hz)' w l ls 2 lc 'black'


unset multiplot
